// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel ComputeBrushedIndicesArray
#include "UnityCG.cginc"

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
float4x4 W_Matrix;
float rand;

float _size;

//*******************
// RANGE FILTERING
//*******************

float _MinX;
float _MaxX;
float _MinY;
float _MaxY;
float _MinZ;
float _MaxZ;

// ********************
// Normalisation ranges
// ********************

float _MinNormX;
float _MaxNormX;
float _MinNormY;
float _MaxNormY;
float _MinNormZ;
float _MaxNormZ;

float3 pointer1;
float3 pointer2;

float width;
float height;
float depth;

float RadiusSphere;

int BrushMode;
int SelectionMode;

bool HasFreeBrushReset;

RWStructuredBuffer<float3> dataBuffer;
StructuredBuffer<float> filteredIndicesBuffer;
RWStructuredBuffer<int> brushedIndicesBuffer;

// Brush Circle
bool Brush2DInCircle(float3 center, float radius, float4 testPoint)
{
	float4 projectedCenter = mul(W_Matrix, float4(center, 1.0));

	return sqrt(pow(testPoint.x - projectedCenter.x, 2) + pow(testPoint.y - projectedCenter.y, 2)) < radius;
}

// Brush Sphere
bool BrushSphere(float3 center, float radius, float3 testPoint)
{
	float3 displacementToCenter = testPoint - center;
	float radiusSqr = radius * radius;
	bool intersects = dot(displacementToCenter, displacementToCenter) < radiusSqr;

	return intersects;
}

bool BrushCube(float3 TFL, float3 BBR, float3 testPoint)
{
	return (testPoint.x >= TFL.x && testPoint.x <= BBR.x
		&&  testPoint.y <= TFL.y && testPoint.y >= BBR.y
		&&  testPoint.z >= TFL.z && testPoint.z <= BBR.z);
}

bool BrushSquare(float3 tl, float3 tr, float3 bl, float3 br, float4 testPoint)
{
	float4 tlp = mul(W_Matrix, float4(tl, 1.0));
	float4 trp = mul(W_Matrix, float4(tr, 1.0));
	float4 blp = mul(W_Matrix, float4(bl, 1.0));
	float4 brp = mul(W_Matrix, float4(br, 1.0));

	return (testPoint.x >= tlp.x && testPoint.x <= trp.x
		&&  testPoint.y <= tlp.y && testPoint.x >= blp.y);
}

// Selects the correct brush type. 1 : cube, 2 : square
bool Brush(int brushType, float3 filteredPosition)
{
	switch (brushType)
	{
		// Brush sphere
		case 0:
			return BrushSphere(float3(pointer1.x, pointer1.y, pointer1.z), RadiusSphere, filteredPosition);

		// Brush cube
		case 1:
			float3 TFL;
			float3 BBR;

			TFL.x = min(pointer1.x, pointer2.x);
			BBR.x = max(pointer1.x, pointer2.x);
			TFL.y = max(pointer1.y, pointer2.y);
			BBR.y = min(pointer1.y, pointer2.y);
			TFL.z = min(pointer1.z, pointer2.z);
			BBR.z = max(pointer1.z, pointer2.z);

			return BrushCube(TFL, BBR, filteredPosition);

		// Brush square
		// case 2:
			// retun BrushSquare(pointer1x, pointer1y,pointer2x,pointer2y,filteredPosition);

		default:
			return false;
	}
}

// TODO
// brush sphere
//brush rectangle
//brush cube
//texturebrush
// brush histogram

//*********************************
// helper functions
//*********************************

// Handles situations where the upper range value is less than the lower range value, specifically when handling negative ranges
bool IsBetweenRanges(float value, float lower, float upper)
{
	if (lower <= upper)
	{
		return (lower <= value && value <= upper);
	}
	else
	{
		return (upper <= value && value <= lower);
	}
}

float NormaliseValue(float value, float i0, float i1, float j0, float j1)
{
	float L = (j0 - j1) / (i0 - i1);
	return (j0 - (L * i0) + (L * value));
}

[numthreads(32, 32, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
	int idx = id.x + id.y * _size;

	// Ignore indices that are filtered
	if (!filteredIndicesBuffer[idx])
	{
		float3 filteredPosition = float3(
			NormaliseValue(dataBuffer[idx].x, _MinNormX, _MaxNormX, 0, width),
			NormaliseValue(dataBuffer[idx].y, _MinNormY, _MaxNormY, 0, height),
			NormaliseValue(dataBuffer[idx].z, _MinNormZ, _MaxNormZ, 0, depth));

		if (IsBetweenRanges(filteredPosition.x, _MinX, _MaxX * width) &&
			IsBetweenRanges(filteredPosition.y, _MinY, _MaxY * height) &&
			IsBetweenRanges(filteredPosition.z, _MinZ, _MaxZ * depth)
		)
		{
			if (Brush(BrushMode, filteredPosition))
			{
				switch (SelectionMode) {
					// Free selection
					case 0:
					// Additive selection
					case 1:
						Result[id.xy] = float4(1.0, 0.0, 0.0, 1.0);  // brushed
						break;

					// Subtractive selection
					case 2:
					default:
						Result[id.xy] = float4(0.0, 0.0, 0.0, 1.0);  // not brushed
						break;
				}
			}
			// Free selection un-brushes points
			else if (SelectionMode == 0 && !HasFreeBrushReset)
			{
				Result[id.xy] = float4(0.0, 0.0, 0.0, 1.0);  // not brushed
			}
		}
		else
		{
			Result[id.xy] = float4(0.0, 0.0, 0.0, 1.0);  // not brushed
		}
	}
}

[numthreads(32, 1, 1)]
void ComputeBrushedIndicesArray(uint3 id : SV_DispatchThreadID)
{
	int x = id.x % _size;
	int y = floor(id.x / _size);
	float2 pos = float2(x, y);

	if (Result[pos].x > 0.0)
		brushedIndicesBuffer[id.x] = 1;
	else
		brushedIndicesBuffer[id.x] = -1;
}